local repo plugin_name plugin_dir initfile initfiles
ZPLUGINDIR=${ZPLUGINDIR:-${ZDOTDIR:-$HOME/.config/zsh}/plugins}
for repo in $@; do
  plugin_name=${repo:t}
  plugin_dir=$ZPLUGINDIR/$plugin_name
  initfile=$plugin_dir/$plugin_name.plugin.zsh
  if [[ ! -e $initfile ]]; then
    initfiles=(
      $plugin_dir/$plugin_name.zsh(N)
      $plugin_dir/init.zsh(N)
      $plugin_dir/*.plugin.{z,}sh(N)
      $plugin_dir/*.{z,}sh{-theme,}(N)
    )
    [[ ${#initfiles[@]} -gt 0 ]] || { echo >&2 "plugin-initfile: No init file found '$repo'." && continue }
    ln -s "${initfiles[1]}" "$initfile"
  fi
  if [[ -e $initfile ]] && [[ ! -e $plugin_dir/init.zsh ]]; then
    ln -s $initfile $plugin_dir/init.zsh
  fi
done
