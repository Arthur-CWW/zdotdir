local repo plugin_name plugin_dir initfile initfiles
ZPLUGINDIR=${ZPLUGINDIR:-${ZDOTDIR:-$HOME/.config/zsh}/plugins}
for repo in $@; do
  plugin_name=${repo:t}
  plugin_dir=$ZPLUGINDIR/$plugin_name
  if [[ ! -d $plugin_dir ]]; then
    echo "Cloning $repo"
    git clone -q --depth 1 --recursive --shallow-submodules https://github.com/$repo $plugin_dir

    initfile=$plugin_dir/init.zsh
    if [[ ! -e $initfile ]]; then
      initfiles=(
        $plugin_dir/$plugin_name.zsh(N)
        $plugin_dir/$plugin_name.plugin.zsh(N)
        $plugin_dir/*.plugin.{z,}sh(N)
        $plugin_dir/*.{z,}sh{-theme,}(N)
      )
      [[ ${#initfiles[@]} -gt 0 ]] || { echo >&2 "Plugin has no init file '$repo'." && continue }
      ln -s "${initfiles[1]}" "$initfile"
    fi
  fi
done
