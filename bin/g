#!/usr/bin/env zsh

function _g_clone() {
  local repo="${@[-1]}"
  if [[ $repo != git://* &&
        $repo != https://* &&
        $repo != http://* &&
        $repo != ssh://* &&
        $repo != git@*:*/* ]];
  then
    if [[ $repo == */* ]]; then
      repo="git@github.com:${repo%.git}.git"
    else
      repo="git@github.com:mattmc3/${repo}.git"
    fi
    command git ${@[@]:1:-1} $repo
  else
    command git $@
  fi
}

function _g_pushup() {
  # git push should just do this, but it doesn't
  # when you create a new branch, push it upstream
  git push --set-upstream origin $(git rev-parse --abbrev-ref HEAD)
}

main() {
  # No arguments: `git status`
  # With arguments: acts like `git`
  if [[ $# -eq 0 ]]; then
    command git status -sb
  elif (( $+functions[_g_${1}] )); then
    _g_${1} "$@"
    return $?
  else
    command git $@
  fi
}
main $@
