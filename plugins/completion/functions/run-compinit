setopt local_options extended_glob

local o_force=()
if [[ "$1" == "-f" ]] || [[ "$1" == "--force" ]]; then
  o_force=($1); shift
fi

if [[ -n "$1" ]]; then
  ZSH_COMPDUMP="$1"
else
  : ${ZSH_COMPDUMP:=${XDG_CACHE_HOME:=$HOME/.cache}/zsh/compdump}
fi

# Load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
autoload -Uz compinit
local comp_files=($ZSH_COMPDUMP(Nmh-20))
if (( $#comp_files )) && ! (( $#o_force )); then
  compinit -i -C -d "$ZSH_COMPDUMP"
else
  compinit -i -d "$ZSH_COMPDUMP"
  # Ensure $ZSH_COMPDUMP is younger than the cache time even if it isn't regenerated.
  touch "$ZSH_COMPDUMP"
fi

# Compile zcompdump, if modified, in background to increase startup speed.
(
  if [[ -s "$ZSH_COMPDUMP" && (! -s "${ZSH_COMPDUMP}.zwc" || "$ZSH_COMPDUMP" -nt "${ZSH_COMPDUMP}.zwc") ]]; then
    zcompile "$ZSH_COMPDUMP"
  fi
) &!

# vim: ft=zsh
