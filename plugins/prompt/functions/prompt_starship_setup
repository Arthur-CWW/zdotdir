#!/bin/zsh
#function prompt_starship_setup {

if ! (( $+commands[starship] )); then
  if (( $+commands[brew] )); then
    command brew install starship
  else
    echo >&2 "Starship prompt not installed. See https://starship.rs"
    return 1
  fi
fi

# When loaded through the prompt command, these prompt_* options will be enabled
prompt_opts=(cr percent sp subst)

if [[ -n "$1" ]]; then
  local -a configs=(
    $ZDOTDIR/themes/$1.toml(N)
    ${XDG_CONFIG_HOME:-$HOME/.config}/starship/$1.toml(N)
  )
  (( $#configs )) && export STARSHIP_CONFIG=$configs[1]
fi

local starship_init=${XDG_CACHE_HOME:=~/.cache}/zsh/starship_init.zsh
local cache=($starship_init(Nmh-20))
[[ -d $starship_init:h ]] || mkdir -p $starship_init:h
(( $#cache )) || starship init zsh --print-full-init >| $starship_init
source $starship_init
#}
